!!!thudiniska.lexc!!!

Multichar_Symbols +SGNOM +SGACC +SGDAT +SGGEN +PLNOM +PLACC +PLDAT +PLGEN
                  +PRS +PST +FUT +IMP
                  +1SG +2SG +3SG +1PL +2PL +3PL
                  +ACTV +PASS +MIDL
                  PSTFUT+ IPFV+ NEG+
                  @P.PSTFUT.ON@ @D.PSTFUT@
                  @P.IPFV.ON@ @D.IPFV@
                  @P.SUBJ.ON@ @D.SUBJ@

LEXICON Root

Noun;
Verb;
Vprfx;

LEXICON Noun

long    Ninfl;
tír     Ninfl;
múr     Ninfl;
bleð    Ninfl;
báhrs   Ninfl;
cáerang Ninfl;
ben+SGNOM:ben   #;
ben+SGACC:ben   #;
ben+SGDAT:beni  #;
ben+SGGEN:benar #;
ben+PLNOM:benir #;
ben+PLACC:benir #;
ben+PLDAT:benum #;
ben+PLGEN:bena  #;
feme    Ninfl;
bláþ    Ninfl;
cnáim   Ninfl;
kló     Ninfl;
skorpu  Ninfl;
cú      Ninfl;

LEXICON Ninfl

+SGNOM:0        #;
+SGACC:0        #;
+SGDAT:^i       #;
+SGGEN:&sggen   #;
+PLNOM:&plnom   #;
+PLACC:&placc   #;
+PLDAT:^um      #;
+PLGEN:^a       #;

LEXICON Verb

ata     Vtense;
tiga    Vtense;
dale    Vtense;
selge   Vtense;
biaða   Vtense;
eþae    Vtense;
ako     Vtense;
légado  Vtense;
fáelae  Vtense;

LEXICON Vtense

@D.PSTFUT@+PRS:@D.PSTFUT@0                 PRSprsn;
@D.PSTFUT@+PST:@D.PSTFUT@0                 PSTprsn;
@D.IPFV@+FUT:@D.IPFV@0                     FUTprsn;
@D.PSTFUT@@D.IPFV@@D.SUBJ@+IMP:@D.PSTFUT@@D.IPFV@@D.SUBJ@0 IMPprsn;

LEXICON PRSprsn

+1SG:0       Vsufx;
+2SG:^r      Vsufx;
+3SG:^d      Vsufx;
+1PL:~m      Vsufx;
+2PL:~ð      Vsufx;
+3PL:~       Vsufx;

LEXICON PSTprsn
+1SG:^ð~     Vsufx;
+2SG:^ð~r    Vsufx;
+3SG:^ð~d    Vsufx;
+1PL:^ð-m    Vsufx;
+2PL:^ð~ð    Vsufx;
+3PL:^ð-     Vsufx;

LEXICON FUTprsn
+1SG:^d~     Vsufx;
+2SG:^d~r    Vsufx;
+3SG:^d~d    Vsufx;
+1PL:^d-m    Vsufx;
+2PL:^d-ð    Vsufx;
+3PL:^d-     Vsufx;

LEXICON IMPprsn
+1SG:`       Vsufx;
+2SG:`r      Vsufx;
+3SG:`d      Vsufx;
+1PL:`~m     Vsufx;
+2PL:`~ð     Vsufx;
+3PL:`~      Vsufx;

LEXICON Vsufx

+ACTV:0         #;
+PASS:^g        #;
+MIDL:^s        #;

LEXICON Vprfx

@P.PSTFUT.ON@PSTFUT+:@P.PSTFUT.ON@gu^     Verb;
@P.IPFV.ON@IPFV+:@P.IPFV.ON@fu^       Verb;
NEG+:nu^        Verb;
@P.SUBJ.ON@SUBJ+:@P.SUBJ.ON@ru^       Verb;
